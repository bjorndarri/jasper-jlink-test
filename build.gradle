plugins {
    id "application"
    id "org.beryx.jlink" version "3.0.1"
    id "org.gradlex.extra-java-module-info" version "1.8"
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation("net.sf.jasperreports:jasperreports:7.0.0") {
        exclude group: "org.apache.xmlgraphics"
        exclude group: "xml-apis"
    }
    implementation("net.sf.jasperreports:jasperreports-pdf:7.0.0") {
        exclude group: "net.sf.jasperreports"
    }
}

extraJavaModuleInfo {
    module("net.sf.jasperreports:jasperreports", "jasperreports") {
        exportAllPackages()
        requires("java.sql")
        requires("java.desktop")
        requires("org.apache.commons.logging")
        requires("org.apache.commons.collections4")
    }

    module("net.sf.jasperreports:jasperreports-pdf", "jasperreports.pdf") {
        exportAllPackages()
        requires("jasperreports")
    }

    automaticModule("commons-beanutils:commons-beanutils", "commons.beanutils")
    automaticModule("commons-collections:commons-collections", "commons.collections")
    automaticModule("com.adobe.xmp:xmpcore", "xmpcore")
}

application {
    getMainModule().set("is.test.jasper")
    getMainClass().set("is.test.jasper.Test")
}